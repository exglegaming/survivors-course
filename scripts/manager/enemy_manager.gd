extends Node


const SPAWN_RADIUS: float = 375.0

@export var basic_enemy_scene: PackedScene
@export var arena_time_manager: ArenaTimeManager

var base_spawn_time: float = 0.0

@onready var timer: Timer = $Timer


func _ready() -> void:
	base_spawn_time = timer.wait_time
	timer.timeout.connect(_on_timer_timeout)
	arena_time_manager.arena_difficulty_increased.connect(_on_arena_difficulty_increased)


func _on_timer_timeout() -> void:
	timer.start()

	var player: Player = get_tree().get_first_node_in_group("player")
	if player == null: return

	var random_direction: Vector2 = Vector2.RIGHT.rotated(randf_range(0.0, TAU))
	var spawn_position: Vector2 = player.global_position + (random_direction * SPAWN_RADIUS)

	var enemy: Enemy = basic_enemy_scene.instantiate()

	var entities_layer: Node2D = get_tree().get_first_node_in_group("entities_layer")
	entities_layer.add_child(enemy)
	enemy.global_position = spawn_position


func _on_arena_difficulty_increased(arena_difficulty: int) -> void:
	var time_off: float = (.1 / 12) * arena_difficulty
	time_off = min(time_off, .7)
	print(time_off)
	timer.wait_time = base_spawn_time - time_off
